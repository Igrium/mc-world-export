plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	repositories {
		ivy {
			setName("ReplayMod")
			setUrl("https://minio.replaymod.com/replaymod/")
			patternLayout { artifact("[artifact]-[revision](-[classifier])(.[ext])") }
			metadataSources { artifact() }
			content { includeGroup("com.replaymod") }
		}
		maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
		maven { url 'https://jitpack.io' }
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Replay Mod
	modImplementation "maven.modrinth:replaymod:${project.replaymod_version}"
	implementation("com.github.LlamaLad7:MixinExtras:0.1.1")
    annotationProcessor("com.github.LlamaLad7:MixinExtras:0.1.1")
	
	implementation(include('de.javagl:obj:0.3.0'))
	implementation('org.joml:joml:1.10.2')

	testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

test {
    useJUnitPlatform()
}

// Thanks to Modmuss for helping with this task!
tasks.register('addon', Zip) {
    from "addon"
    archiveBaseName = "import_vcap"
    destinationDirectory = project.layout.buildDirectory.dir("libs")
    
    filesMatching("**/__init__.py") {
        filter {
            it.replace("(0, 0, 0)", "(${version.replaceAll("[^\\d.]", "").split("\\.").join(", ")})")
            
        }
    }

    exclude "**/__pycache__*"
    exclude "*.py[cod]"
    exclude "*.py.class"
}